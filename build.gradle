buildscript {
    repositories {
        mavenCentral()
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        classpath 'org.jetbrains.kotlin:kotlin-gradle-plugin:1.3.50'
        classpath 'org.unbroken-dome.gradle-plugins:gradle-testsets-plugin:2.1.1'
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.8.4'
        classpath 'com.diffplug.spotless:spotless-plugin-gradle:3.24.0'
        classpath 'com.adarshr:gradle-test-logger-plugin:1.7.0'
    }
}

description = 'Integrates web3j with the Corda blockchain platform.'

ext {
    loadPropsFromFile = { String path ->
        def props = new Properties()
        def propsFile = file(path)
        if (propsFile.exists()) {
            propsFile.withReader { reader ->
                props.load(reader)
            }
        }
        props
    }
    versions = loadPropsFromFile("$rootDir/versions.properties")
    ossrhUsername = project.hasProperty('ossrhUsername') ? project.property('ossrhUsername') : System.getenv('OSSRH_USERNAME')
    ossrhPassword = project.hasProperty('ossrhPassword') ? project.property('ossrhPassword') : System.getenv('OSSRH_PASSWORD')
    ossrhRepoUrl = project.hasProperty('ossrhRepoUrl') ? project.property('ossrhRepoUrl') : System.getenv('OSSRH_REPO_URL')
    ossrhSnapshotRepoUrl = project.hasProperty('ossrhSnapshotRepoUrl') ? project.property('ossrhSnapshotRepoUrl') : System.getenv('OSSRH_SNAPSHOT_REPO_URL')
    bintrayUser = project.hasProperty('bintrayUser') ? project.property('bintrayUser') : System.getenv('BINTRAY_USER')
    bintrayKey = project.hasProperty('bintrayApiKey') ? project.property('bintrayApiKey') : System.getenv('BINTRAY_API_KEY')
    bintrayGpgPassphrase = project.hasProperty('bintrayGpgPassphrase') ? project.property('bintrayGpgPassphrase') : System.getenv('BINTRAY_GPG_PASSPHRASE')
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'idea'
    apply plugin: 'maven'
    apply plugin: 'signing'
    apply plugin: 'maven-publish'
    apply plugin: 'com.jfrog.bintray'
    apply plugin: 'org.jetbrains.kotlin.jvm'
    apply plugin: 'org.unbroken-dome.test-sets'
    apply plugin: 'com.diffplug.gradle.spotless'
    apply plugin: 'com.adarshr.test-logger'

    repositories {
        jcenter()
        mavenCentral()
        gradlePluginPortal()
        maven { url 'https://repo.gradle.org/gradle/libs-releases' }
        maven { url 'https://ci-artifactory.corda.r3cev.com/artifactory/corda-releases' }
        maven { url 'http://ci-artifactory.corda.r3cev.com/artifactory/corda-dependencies' }
        maven { url 'https://dl.bintray.com/r3/corda/' }
        maven {
            url 'https://oss.sonatype.org/content/repositories/snapshots/'
            mavenContent {
                snapshotsOnly()
            }
        }
    }

    task sourcesJar(type: Jar) {
        from sourceSets.main.allSource
        classifier = 'sources'
    }

    testSets {
        integrationTest {
            dirName = 'integration-test'
        }
    }

    tasks.withType(Test) {
        useJUnitPlatform()
        systemProperties = [
                'junit.jupiter.extensions.autodetection.enabled': 'true',
                'junit.jupiter.testinstance.lifecycle.default'  : 'per_class'
        ]
    }

    configurations {
        testOutput.extendsFrom(testCompile)
        integrationTestOutput.extendsFrom(integrationTestCompile)
    }

    artifacts {
        archives sourcesJar
        testOutput testJar
        integrationTestOutput integrationTestJar
    }

    testlogger {
        showStandardStreams true
    }

    dependencies {
        implementation 'org.jetbrains.kotlin:kotlin-stdlib-jdk8'
        implementation 'org.jetbrains.kotlin:kotlin-reflect'
        implementation 'org.jetbrains.kotlin:kotlin-noarg'
        implementation 'org.jetbrains.kotlin:kotlin-native-utils'

        implementation("org.jetbrains:annotations:${versions.annotations}") { force = true }

        implementation("org.slf4j:slf4j-api:${versions.slf4j}") { force = true }
        implementation("org.slf4j:slf4j-ext:${versions.slf4j}") { force = true }
        implementation("org.slf4j:jcl-over-slf4j:${versions.slf4j}") { force = true }
        implementation("org.slf4j:jul-to-slf4j:${versions.slf4j}") { force = true }

        // FIXME Move to specific modules, not all require those dependencies
        implementation group: 'com.fasterxml.jackson.module', name: 'jackson-module-kotlin', version: versions.jackson, force: true
        implementation group: 'com.fasterxml.jackson.module', name: 'jackson-module-jaxb-annotations', version: versions.jackson, force: true
        implementation group: 'com.fasterxml.jackson.core', name: 'jackson-core', version: versions.jackson, force: true
        implementation group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version: versions.jackson, force: true
        implementation group: 'com.fasterxml.jackson.core', name: 'jackson-annotations', version: versions.jackson, force: true
        implementation group: 'com.fasterxml.jackson.dataformat', name: 'jackson-dataformat-yaml', version: versions.jackson, force: true
        implementation group: 'org.glassfish.jersey.inject', name: 'jersey-hk2', version: versions.jersey, force: true
        implementation group: 'org.glassfish.jersey.core', name: 'jersey-client', version: versions.jersey, force: true
        implementation group: 'org.glassfish.jersey.media', name: 'jersey-media-json-jackson', version: versions.jersey, force: true
        implementation group: 'org.glassfish.jersey.ext', name: 'jersey-proxy-client', version: versions.jersey, force: true
        implementation group: 'org.glassfish.jersey.containers', name: 'jersey-container-servlet-core', version: versions.jersey, force: true
        implementation group: 'org.glassfish.jersey.containers', name: 'jersey-container-jetty-http', version: versions.jersey, force: true
        implementation group: 'org.glassfish.jersey.containers', name: 'jersey-container-servlet', version: versions.jersey, force: true

        implementation("net.bytebuddy:byte-buddy:1.9.10") { force = true }
        implementation("com.google.errorprone:error_prone_annotations:2.2.0") { force = true }
        implementation("com.google.guava:guava:28.1-jre") { force = true }
        implementation("commons-io:commons-io:2.6") { force = true }

        implementation "io.github.microutils:kotlin-logging:${versions.kLogging}"

        implementation("com.pinterest.ktlint:ktlint-core:${versions.ktlint}") {
            exclude group: 'org.apache.commons'
            exclude group: 'org.apache.httpcomponents'
            exclude group: 'org.slf4j'
        }
        implementation "com.pinterest.ktlint:ktlint-ruleset-standard:${versions.ktlint}"
        implementation "com.pinterest.ktlint:ktlint-ruleset-experimental:${versions.ktlint}"

        testImplementation "org.junit.jupiter:junit-jupiter-api:${versions.junit5}"
        testImplementation "com.willowtreeapps.assertk:assertk-jvm:${versions.assertk}"
        testImplementation "junit:junit:${versions.junit4}"
        testImplementation("io.mockk:mockk:${versions.mockk}") {
            exclude group: 'org.jetbrains.kotlin'
        }

        testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:${versions.junit5}"
        testRuntimeOnly "org.junit.vintage:junit-vintage-engine:${versions.junit5}"
        testRuntimeOnly "org.jetbrains.kotlin:kotlin-script-util"

        integrationTestImplementation "org.testcontainers:testcontainers:${versions.testcontainers}"
        integrationTestImplementation "org.testcontainers:junit-jupiter:${versions.testcontainers}"
    }

    sourceCompatibility = JavaVersion.VERSION_1_8
    compileKotlin.kotlinOptions.jvmTarget = sourceCompatibility
    compileTestKotlin.kotlinOptions.jvmTarget = sourceCompatibility
    compileIntegrationTestKotlin.kotlinOptions.jvmTarget = sourceCompatibility

    spotless {
        kotlin {
            // This path needs to be relative to each project
            target fileTree('.') {
                include '**/*.kt'
                exclude '**/.gradle/**'
            }
//            FIXME spotless breaks with ktlint version 0.32 + (https://github.com/diffplug/spotless/issues/419)
//            ktlint(versions.ktlint)
            trimTrailingWhitespace()
            licenseHeaderFile "${rootDir}/gradle/spotless.license"
            endWithNewline()
        }
        java {
            // This path needs to be relative to each project
            target fileTree('.') {
                include '**/src/*/java/**/*.java'
                exclude '**/.gradle/**'
                exclude '**/generated/**'
            }
            removeUnusedImports()
            googleJavaFormat("1.7").aosp()
            importOrder 'java', '', 'org.web3j', '\\#'
            trimTrailingWhitespace()
            endWithNewline()
            licenseHeaderFile "${rootDir}/gradle/spotless.license"
            paddedCell()
        }
    }

    publishing {
        publications {
            maven(MavenPublication) {
                groupId = project.group
                artifactId = project.name
                version = project.version
                from components.java
                artifact sourcesJar
                artifact integrationTestJar
                pom {
                    name = project.name
                    description = project.description
                    url = "https://github.com/web3j/web3j-corda"
                    licenses {
                        license {
                            name = "The Apache License, Version 2.0"
                            url = "http://www.apache.org/licenses/LICENSE-2.0.txt"
                            distribution = "repo"
                        }
                    }
                    developers {
                        developer {
                            id = 'xaviarias'
                            name = 'Xavier Arias'
                            email = 'xavier@web3labs.com'
                            organization = 'Web3 Labs'
                        }
                    }
                    scm {
                        connection = "scm:git:https://github.com/web3j/web3j-corda.git"
                        developerConnection = "scm:git://github.com/web3j/web3j-corda.git"
                        url = "https://github.com/web3j/web3j-corda.git"
                    }
                }
            }
        }
    }

    uploadArchives {
        repositories {
            mavenDeployer {
                repository(url: ossrhRepoUrl) {
                    authentication(userName: ossrhUsername, password: ossrhPassword)
                }
                snapshotRepository(url: ossrhSnapshotRepoUrl) {
                    authentication(userName: ossrhUsername, password: ossrhPassword)
                }
                pom.project {
                    packaging 'jar'
                    name 'web3j Corda Integration'
                    description rootProject.description
                    url 'https://web3j.io'
                    scm {
                        connection 'scm:git:https://github.com/web3j/web3j-corda.git'
                        url 'https://github.com/web3j/web3j-corda.git'
                        developerConnection 'scm:git://github.com/web3j/web3j-corda.git'
                    }
                    licenses {
                        license {
                            name 'The Apache License, Version 2.0'
                            url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                            distribution 'repo'
                        }
                    }
                    developers {
                        developer {
                            id = 'xaviarias'
                            name = 'Xavier Arias'
                            email = 'xavier@web3labs.com'
                            organization = 'Web3 Labs'
                        }
                    }
                }
            }
        }
    }

    bintray {
        user = bintrayUser
        key = bintrayKey
        publications = ['maven']
        publish = true
        pkg {
            repo = 'maven'
            name = project.name
            desc = project.description
            userOrg = 'web3j'
            licenses = ['Apache-2.0']
            issueTrackerUrl = 'https://github.com/web3j/web3j-corda/issues'
            vcsUrl = 'https://github.com/web3j/web3j-corda.git'
            websiteUrl = 'https://web3j.io'
            publicDownloadNumbers = true
            version {
                gpg {
                    sign = true
                    passphrase = bintrayGpgPassphrase
                }
                mavenCentralSync {
                    sync = true
                    user = ossrhUsername
                    password = ossrhPassword
                    close = '1'
                }
            }
        }
    }
}